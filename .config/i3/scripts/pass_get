#!/usr/bin/env fish

test -z $PASS_SUBNAME_SPLIT_CHAR; and set PASS_SUBNAME_SPLIT_CHAR '%'
test -z $PASSWORD_STORE_DIR; and set PASSWORD_STORE_DIR $HOME/.password-store
test -z $PASS_FALLBACK_USER; and set PASS_FALLBACK_USER "User"

# get name of currently active window
test -z "$argv"; and set -l argv ""
set -l active_line (string match -ri '^_NET_ACTIVE_WINDOW\(WINDOW\).+' (xprop -root))
set -l active_id (string replace -r '^[^#]+#\s(\S+)' '$1' "$active_line")
set -l name_line (string match -r 'WM_NAME\([^)]+\).+' (xprop -id $active_id))
set -l class_line (string match -r 'WM_CLASS\([^)]+\).+' (xprop -id $active_id))
set -l name (string match -r '"[^"]+"$' "$name_line")
set -l class (string match -r '"[^"]+"$' "$class_line")
set name (string replace -a ' ' '_' "$name")
set class (string replace -a ' ' '_' "$class")


function get_value -a pass key copy fallback
    # get the value for $key in the pass file
    pass show $pass | string replace -rf '^'$key':\s?(.+)$' '$1' | read -l value
    test -z "$value"; and set value "$fallback"
    if test -n "$value"
        if eval $copy
            echo $value | xclip -selection clipboard -i
            true
        else
            echo $value
            true
        end
    else
        false
    end
end


function complain_not_found -a key
    notify-send "Unable to retrieve $key"
end


function xdo_pass_entry -a pass
    set -l user (get_value $pass "user" false "$PASS_FALLBACK_USER"); or return
    set -l pass_string (pass show $pass | head -n 1); or return
    set -l field_div (get_value $pass "xdoDiv" false)
    test -z "$field_div"; and set field_div "Tab"
    xdotool keyup Super
    xdotool type --clearmodifiers $user
    xdotool keyup Super
    xdotool key --clearmodifiers (string split " " $field_div)
    xdotool keyup Super
    xdotool type --clearmodifiers $pass_string
    xdotool keyup Super
    xdotool key --clearmodifiers Return
end


function loop_candidates -S -a prop
    # loop through candidates and find one that matches $prop
    for password in $passes
        set -l barepass (command basename $password)
        if string match -qie "$barepass" $prop
            set candidates $candidates $password
            continue
        end

        set -l subs (string split "$PASS_SUBNAME_SPLIT_CHAR" "$barepass")
        if [ ! "$subs" = "$barepass" ]
            for sub in $subs
                if not contains $password $candidates;
                   and string match -qie "$sub" $prop
                    set candidates $candidates $password
                    continue
                end
            end
        end
    end
end


# get list of all pass files
set -l passes (string replace -r '^.+?\.password-store/(.+?)\.gpg$' '$1' $PASSWORD_STORE_DIR/**/*)

# loop through files and find one that matches based on name property or if
# that doesn't work class property
set -l candidates
loop_candidates $name
if test -z "$candidates"
    loop_candidates $class
end

# send candidate to pass; use dmenu to select one if there are too many
set -l cand_num (count $candidates)
set -l final_candidate

if test $cand_num -eq 1
    # if there was only one password found set that to final candidate
    set final_candidate $candidates
    notify-send "Found $final_candidate."
else if test $cand_num -gt 1
    # if there's more than one, select one with dmenu
    notify-send 'Too many candidates.'
    for candidate in $candidates
        printf '%s\n' $candidate
    end | dmenu | read final_candidate
else 
    # if there's none, also select one with dmenu
    notify-send 'Nothing found. Please select manually.'
    for candidate in $passes
        printf '%s\n' $candidate
    end | dmenu | read final_candidate
end

if test -n "$final_candidate"
    # handle different command flags
    switch $argv
        case "-l"
            get_value $final_candidate "user" true "$PASS_FALLBACK_USER"; or exit 1
        case "-x"
            xdo_pass_entry $final_candidate; or exit 1
        case "*"
            pass show -c $final_candidate; or exit 1
    end
    and notify-send -u low "Retrieved data from $final_candidate."
else
    notify-send 'No password selected.'
end
